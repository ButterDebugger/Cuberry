plugins {
    id 'java'
    id "xyz.jpenilla.run-velocity" version "2.3.0"
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "mrnavastar-releases"
        url = "https://maven.mrnavastar.me/releases"
    }
}

dependencies {
    compileOnly "com.velocitypowered:velocity-api:${project.velocity_version}"
    annotationProcessor "com.velocitypowered:velocity-api:${project.velocity_version}"
    implementation "dev.dejvokep:boosted-yaml:1.3.5"
    compileOnly "me.mrnavastar.protoweaver:proxy:${project.protoweaver_version}"

    implementation project(':common')
}

tasks.register('runRebuiltVelocity') {
    dependsOn ':package'
    finalizedBy 'runVelocity'
}

runVelocityExtension {
    disablePluginJarDetection()
}

tasks {
    runVelocity {
        pluginJars(file("$rootDir/build/libs/$project_name-$project_version-Universal.jar"))
        velocityVersion(project.velocity_version)
        downloadPlugins {
            url("https://maven.mrnavastar.me/releases/me/mrnavastar/protoweaver/proxy/$protoweaver_version/proxy-$protoweaver_version..jar")
        }
    }
}
