plugins {
    id 'java'
    id "xyz.jpenilla.run-paper" version "2.3.0"
    id "io.papermc.paperweight.userdev" version "2.0.0-beta.16"
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "placeholderapi-repo"
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = "mrnavastar-releases"
        url = "https://maven.mrnavastar.me/releases"
    }
}

dependencies {
    // Paper
    paperweight.paperDevBundle(project.paper_version)

    // Libraries
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly "me.mrnavastar.protoweaver:common:${project.protoweaver_version}"

    // Common
    implementation project(':common')
}

processResources {
    def props = [
            'version': version,
            'project_id': project.project_id,
            'project_name': project.project_name,
            'project_description': project.project_description,
            'project_url': project.project_url,
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml', 'config.yml']) {
        expand props
    }
}

tasks.register('runRebuiltServer') {
    dependsOn ':package'
    finalizedBy 'runServer'
}

runPaper {
    disablePluginJarDetection()
}

tasks {
    runServer {
        pluginJars(file("$rootDir/build/libs/$project_name-mc$minecraft_version-$project_version-Universal.jar"))
        minecraftVersion(project.minecraft_version)
        downloadPlugins {
            url("https://maven.mrnavastar.me/releases/me/mrnavastar/protoweaver/paper/$protoweaver_version/paper-$protoweaver_version-dev.jar")
        }
    }
    assemble {
        dependsOn(tasks.reobfJar)
    }
}
