plugins {
    id 'java'
    id "xyz.jpenilla.run-paper" version "2.3.1"
    id "io.papermc.paperweight.userdev" version "2.0.0-beta.16"
}

dependencies {
    // Paper
    paperweight.paperDevBundle(paper_version)

    // Libraries
    compileOnly "me.clip:placeholderapi:$placeholderapi_version"
    compileOnly "me.mrnavastar.protoweaver:common:$protoweaver_version"

    // Common
    implementation project(':common')
}

processResources {
    def props = [
            'version': version,
            'project_id': project_id,
            'project_name': project_name,
            'project_description': project_description,
            'project_url': project_url,
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml', 'config.yml']) {
        expand props
    }
}

tasks.register('runRebuiltServer') {
    dependsOn ':package'
    finalizedBy 'runServer'
}

runPaper {
    disablePluginJarDetection()
}

tasks {
    runServer {
        pluginJars(file("$rootDir/build/libs/$project_name-mc$minecraft_version-$project_version-Universal.jar"))
        minecraftVersion(minecraft_version)
        downloadPlugins {
            url("https://maven.mrnavastar.me/releases/me/mrnavastar/protoweaver/paper/$protoweaver_version/paper-$protoweaver_version-dev.jar")
            hangar("PlaceholderAPI", placeholderapi_version)
        }
    }
    assemble {
        dependsOn(tasks.reobfJar)
    }
}
