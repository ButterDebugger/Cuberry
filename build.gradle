plugins {
    id "java-library"
    id "com.gradleup.shadow" version "9.2.2"
    id "maven-publish"
}

var javaLanguageVersion = JavaLanguageVersion.of(project.java_version)

allprojects {
    group = "dev.debutter.cuberry"
    version = project.plugin_version

    tasks.withType(JavaCompile) {
        options.encoding = "ISO-8859-1"
        options.release = javaLanguageVersion.asInt()
    }

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "mrnavastar-releases"
            url = "https://maven.mrnavastar.me/releases"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            name = "placeholderapi-repo"
            url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
        }
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    publishing {
        publications {
            maven(MavenPublication) {
                groupId "dev.debutter.cuberry"
                artifactId project.name
                version version

                from components.java
            }
        }
    }

    java {
        toolchain.languageVersion.set(javaLanguageVersion)
    }
}

dependencies {
    implementation project(":common")
    implementation project(":paper")
    implementation project(":velocity")
}

tasks.register("package") {
    dependsOn ":clean", ":shadowJar"
}

shadowJar {
    archiveClassifier.set("Universal")
    archiveAppendix.set("mc$minecraft_version")

    relocate "dev.dejvokep.boostedyaml", "dev.debutter.cuberry.libs.boostedyaml"
}
