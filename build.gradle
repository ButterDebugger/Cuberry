plugins {
    id 'java-library'
    id "io.github.goooler.shadow" version "8.1.7"
    id "maven-publish"
}

var javaLanguageVersion = JavaLanguageVersion.of(project.java_version)

allprojects {
    group = 'dev.debutter.cuberry'
    version = project.project_version

    tasks.withType(JavaCompile) {
        options.encoding = "ISO-8859-1"
        options.release = javaLanguageVersion.asInt()
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'dev.debutter.cuberry'
                artifactId = project.name
                version = version

                from components.java
            }
        }
    }

    java {
        toolchain.languageVersion.set(javaLanguageVersion)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':common')
    implementation project(':paper')
    implementation project(':velocity')
}

tasks.register('package') {
    dependsOn ':clean', ':shadowJar'
}

shadowJar {
    archiveClassifier.set('Universal')

    relocate 'dev.dejvokep.boostedyaml', 'dev.debutter.cuberry.libs.boostedyaml'
}
